plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    // For more details visit https://developer.android.com/guide/navigation/navigation-pass-data#Safe-args
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
}

def static getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "pointzi.dishapp"
        minSdkVersion 28
        targetSdkVersion 33
        versionCode 3
        versionName "1.0.0"
        manifestPlaceholders += [app_key: "FavDish"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    productFlavors {
        continuousIntegration {
            flavorDimensions "ci"
            resValue "string", "app_key", "FavDish"
        }
        localDev {
            flavorDimensions "dev"
            resValue "string", "app_key", "FavDish"
        }

        prod {
            flavorDimensions "prod"
            resValue "string", "app_key", "FavDish"
        }

        staging {
            flavorDimensions "staging"
            resValue "string", "app_key", "FavDish_staging"
        }

    }

}
apply from: '../gradle/version.gradle'

dependencies {

    println("Current flavor selected: " + getCurrentFlavor())
    println("SDK Version: " + getContextualSdkVersion())
    if(getCurrentFlavor().contentEquals("localDev")){
        implementation project(":contextual"),  {
            exclude group: 'com.github.bumptech.glide'
            exclude group: 'androidx.room'
        }
    } else if(getCurrentFlavor().contains("prod") || getCurrentFlavor().contains("demo")) {
        implementation(group: "com.pointzi", name: "pointzi", version: getContextualSdkVersion()) {
            exclude group: 'com.github.bumptech.glide'
            exclude group: 'androidx.room'
        }
    } else {
        // This is used by CI(dev and staging)
        implementation(group: "com.pointzi.dev", name: "pointzi", version: getContextualSdkVersion()) {
            exclude group: 'com.github.bumptech.glide'
            exclude group: 'androidx.room'
        }
    }

    implementation 'com.mapbox.maps:android:10.16.1'


    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'

    // SDP - A scalable size unit library
    // https://github.com/intuit/sdp
    implementation 'com.intuit.sdp:sdp-android:1.0.6'

    // Dexter runtime permissions
    // https://github.com/Karumi/Dexter
    implementation 'com.karumi:dexter:6.2.3'

    // Image loading library
    // For more detail visit the link: https://github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    // Room components
    // Reference Link: https://developer.android.com/training/data-storage/room
    def room_version = '2.5.1'
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // These dependencies are already added if you have created the project with BottomNavigationActivity.
    // Lifecycle components
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    implementation 'androidx.palette:palette-ktx:1.0.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'

    // RxJava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.4'

    // Kotlin + coroutines
    def work_version = "2.7.1"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

// https://stackoverflow.com/questions/30621183/how-to-get-current-flavor-in-gradle
// A gradle sync will not get the correct `tskReqStr` variable. A build is required(e.g. assembleLocalDevDebug)
def getCurrentFlavor() {
    Gradle gradle = getGradle()
    String tskReqStr = gradle.getStartParameter().getTaskRequests().toString()
    if (tskReqStr.contains("prod")) {
        return "prod"
    } else if(tskReqStr.contains("LocalDev")) {
        return "localDev"
    } else if(tskReqStr.contains("staging")){
        return "staging"
    } else {
        // Invoked by CI
        return "dev"
    }
}

def getContextualSdkVersion(){
    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
    return properties.getProperty("VERSION_NAME", "2.+")
}